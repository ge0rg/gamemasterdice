// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
	repositories {
		mavenCentral()
		google()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:8.13.2'
	}
}

plugins {
	// obtain revision from git
	id 'org.ajoberstar.grgit' version '5.3.2'
	id 'com.github.triplet.play' version "3.13.0"
}

allprojects {
	repositories {
		mavenCentral()
		google()
	}
}

/////////////// GameMasterDice specific build rules ///////////////
apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'

ext {
	build_date = new Date().format('yyyy-MM-dd')
	build_revision = grgit.describe(tags: true)
	build_version = "${build_revision} ${build_date}"
}

play {
	serviceAccountCredentials = file('play-publish-credentials.json')
	track = 'beta'
}

android {
	namespace 'de.duenndns.gmdice'
	compileSdk 35
	defaultConfig {
		minSdk 11
		targetSdk 35
		versionName "$build_revision"
		versionCode 11
		resValue "string", "build_revision", "$build_revision"
	}

	signingConfigs {
		release {
			storeFile file(RELEASE_STORE_FILE)
			storePassword RELEASE_STORE_PASSWORD
			keyAlias RELEASE_KEY_ALIAS
			keyPassword RELEASE_KEY_PASSWORD
		}
	}

	buildTypes {
		debug {
			minifyEnabled false
		}
		release {
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'
			signingConfig signingConfigs.release
		}
	}

	sourceSets {
		main {
			manifest.srcFile 'AndroidManifest.xml'
			java.srcDirs = ['src']
			resources.srcDirs = ['src']
			aidl.srcDirs = ['src']
			renderscript.srcDirs = ['src']
			res.srcDirs = ['res']
			assets.srcDirs = ['assets']
		}
	}

	lintOptions {
		disable 'MissingTranslation'
	}
}
